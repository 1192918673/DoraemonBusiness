apply plugin: 'com.android.application'


ext {
    TestVersion = '1.0.0.1'
    TestVersionCode = 1000001
    Version = '1.0.0'
    VersionCode = 10000
    DevURLDOMAIN = '"http://doraemon.babadeyan.com/"'
    BetaURLDOMAIN = '"https://ol.api.xhkhealth.com"'
    OnlineURLDOMAIN = '"https://ol.api.xhkhealth.com"'

    DevUMengAppKey = "57a9406be0f55a4be60038ba"
    OnlineUMengAppKey = "57a9405267e58e073100057e"
}

android {
    compileSdkVersion 22
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.geeknewbee.doraemon"
        minSdkVersion 18
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        multiDexEnabled = true
        ndk { abiFilters 'armeabi-v7a' }
        resConfigs "zh"
        buildConfigField "boolean", "NEED_DEBUG", "false"
        buildConfigField "boolean", "HAVE_SPEECH_DEVCE", "false"
        buildConfigField "boolean", "NEED_START_BLE", "true"
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java', "src-dao"]
        }
    }

    signingConfigs {
        myConfig {
            storeFile file("keystore.jks")      //签名文件
            storePassword "111111"
            keyAlias "doraemon"
            keyPassword "111111"  //签名密码
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.myConfig
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.myConfig
            minifyEnabled false
        }
    }


    productFlavors {
        //开发版
        Dev {
            versionName TestVersion
            versionCode TestVersionCode
            buildConfigField "String", "URLDOMAIN", DevURLDOMAIN
            manifestPlaceholders = ["UMENG_APPKEY" : DevUMengAppKey,
                                    "UMENG_CHANNEL": "Fir"]
            buildConfigField "boolean", "NEED_DEBUG", "true"
        }
        //测试版
        Beta {
            versionName TestVersion
            versionCode TestVersionCode
            buildConfigField "String", "URLDOMAIN", BetaURLDOMAIN
            manifestPlaceholders = ["UMENG_APPKEY" : OnlineUMengAppKey,
                                    "UMENG_CHANNEL": "Fir"]
            buildConfigField "boolean", "NEED_DEBUG", "false"
        }

        //发布版
        Online {
            buildConfigField "String", "URLDOMAIN", OnlineURLDOMAIN
            manifestPlaceholders = ["UMENG_APPKEY" : OnlineUMengAppKey,
                                    "UMENG_CHANNEL": "Fir"]
            buildConfigField "boolean", "NEED_DEBUG", "false"
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(output.outputFile.parent, "Doraemon_Business" + variant.productFlavors.get(0).name + "_"
                    + variant.buildType.name + "_" + variant.versionName + ".apk")
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.0'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0'
    compile project(path: ':doraemonsdk')
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'com.android.support:multidex:1.0.1'
    compile 'de.greenrobot:greendao:2.0.0'
    compile 'com.facebook.stetho:stetho:1.1.0'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.google.zxing:core:3.2.1'
}
